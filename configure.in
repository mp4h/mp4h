# Configure template for GNU m4.
# Copyright (C) 1991-1998 Free Software Foundation, Inc.
# Process this file with autoconf to produce a configure script.

AC_INIT(src/mp4h.c)
AC_CONFIG_HEADER(config.h)

shtool=./shtool
PACKAGE=mp4h
VERSION="`$shtool version -l c -d short src/version.c`"
LONG_VERSION="`$shtool version -l c -d long src/version.c`"
$shtool echo -e "Configuring for %B$PACKAGE $LONG_VERSION %b"

AC_SUBST(PACKAGE)
AC_SUBST(VERSION)
AC_DEFINE_UNQUOTED(PACKAGE, "$PACKAGE")
AC_DEFINE_UNQUOTED(VERSION, "$VERSION")

MAINTAINER_MODE_TRUE='#'
AC_SUBST(MAINTAINER_MODE_TRUE)

if test x$datadir = 'x${prefix}/share'; then
    if test x$prefix = xNONE; then
        PKGDATADIR="$ac_default_prefix/share/mp4h"
    else
        PKGDATADIR="$prefix/share/mp4h"
    fi
else
    PKGDATADIR="$datadir/mp4h"
fi
AC_DEFINE_UNQUOTED(PKGDATADIR, "$PKGDATADIR")
pkgdatadir="$PKGDATADIR"
AC_SUBST(pkgdatadir)

if test x$libdir = 'x${exec_prefix}/lib'; then
    if test x$prefix = xNONE; then
        PKGLIBDIR="$ac_default_prefix/lib/mp4h"
    else
        PKGLIBDIR="$prefix/lib/mp4h"
    fi
else
    PKGLIBDIR="$libdir/mp4h"
fi
AC_DEFINE_UNQUOTED(PKGLIBDIR, "$PKGLIBDIR") 
pkglibdir="$PKGLIBDIR"
AC_SUBST(pkglibdir)

AC_ARG_PROGRAM
AC_PROG_INSTALL

AC_PROG_CC
AC_PROG_CPP
AC_PROG_MAKE_SET

AM_WITH_DMALLOC
AM_WITH_MODULES

echo ''
$shtool echo -e "Checking for %Bheaders and libraries%b"
AC_AIX
AC_MINIX
AC_ISC_POSIX

AM_C_PROTOTYPES
AC_C_CONST

AC_HEADER_STDC
AC_CHECK_HEADERS(limits.h locale.h memory.h string.h unistd.h time.h)
AC_CHECK_HEADERS(pwd.h grp.h dirent.h sys/stat.h sys/types.h sys/times.h)
AC_TYPE_SIZE_T

AC_CHECK_FUNCS(mkstemp strerror tmpfile getpwuid getgrgid)

AC_FUNC_ALLOCA
AC_FUNC_VPRINTF
AC_REPLACE_FUNCS(strtol obstack xmalloc xstrdup)
AC_CHECK_FUNC(getopt_long, , [LIBOBJS="$LIBOBJS getopt1.o getopt.o"])

AC_MSG_CHECKING([for HTML postprocessing])
AC_ARG_WITH(tidy,dnl
[  --with-tidy=PATH        post-process HTML documentation with tidy],[dnl
TIDY=$with_tidy
test "$TIDY" = yes && TIDY=tidy
AC_MSG_RESULT([$TIDY])
],[
TIDY="@:"
AC_MSG_RESULT([no])
])
AC_SUBST(TIDY)

echo ''
$shtool echo -e "Checking for %Bloadable modules%b support"
AC_LIBLTDL_CONVENIENCE
AC_ENABLE_SHARED
AC_DISABLE_STATIC
AC_LIBTOOL_DLOPEN
AC_PROG_LIBTOOL
AC_SUBST(INCLTDL)
AC_SUBST(LIBLTDL)
AC_CONFIG_SUBDIRS(libltdl)

AC_OUTPUT([
   Makefile
   lib/Makefile
   libltdl/Makefile
   src/Makefile
   packages/Makefile
   packages/WML/Makefile
   modules/Makefile
   doc/Makefile
   tests/Makefile
])

echo ''
$shtool echo -e "Now please type %Bmake%b to compile mp4h"
echo ''

